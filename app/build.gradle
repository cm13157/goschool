apply plugin: 'com.android.application'

android {
    //指定用来编译的Android API版本号
    compileSdkVersion COMPILE_SDK_VERSION as int
    //指定sdk构建工具版本号
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        //默认配置
        applicationId "com.wty.app.goschool"
        //指定运行需要的最小API版本
        minSdkVersion MIN_SDK_VERSION
        //指定用来测试的API版本
        targetSdkVersion TARGET_SDK_VERSION
        //app版本号
        versionCode VERSION_CODE as int
        //app 版本名字
        versionName VERSION_NAME
        //分包
        multiDexEnabled true
    }

    signingConfigs {
        releaseConfig {
            keyAlias 'wutingyou'
            keyPassword '12345678'
            storeFile file('./keystore/apprelease.jks')
            storePassword '12345678'
        }
    }

    lintOptions {
        //忽略Lint错误
        abortOnError false
    }

    sourceSets{
        main{
            jniLibs.srcDirs=['libs']
        }
    }
    buildTypes {

        debug {
            //debug
        }

        release {
            //启用代码混淆
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //签名
            signingConfig signingConfigs.releaseConfig
            //移除无用的Resource
            shrinkResources true
            //资源文件对齐
            zipAlignEnabled true
        }
    }

    productFlavors {//多渠道打包
        googlepay {}
        baidu {}
        qihoo360 {}
        xiaomi {}
        tencent {}
        anzhi {}
    }

    // 指定apk名字输出格式
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            String apkParent = output.outputFile.parent
            String apkName = "goschool-${variant.buildType.name}-${variant.versionName}-" +
                    "${variant.productFlavors[0].name}.apk".toLowerCase()
            output.outputFile = new File(apkParent,apkName)
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':library')

    //内存泄露检测工具 区分开发、发行版
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'

    compile 'cn.pedant.sweetalert:library:1.3'
}
